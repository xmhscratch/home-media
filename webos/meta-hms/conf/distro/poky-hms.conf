require conf/distro/poky.conf
require conf/distro/include/gcsections.inc

DISTRO = "poky-hms"
DISTRO_NAME = "Poky Home Media"
DISTROOVERRIDES = "poky:poky-hms"
TCLIBC = "musl"

FULL_OPTIMIZATION="-Os -pipe ${DEBUG_FLAGS}"

# FIXME: consider adding a new "tiny" feature
#DISTRO_FEATURES_append = " tiny"

# Distro config is evaluated after the machine config, so we have to explicitly
# set the kernel provider to override a machine config.
PREFERRED_PROVIDER_virtual/kernel = "linux-yocto"
PREFERRED_VERSION_linux-yocto ?= "6.6%"

# We can use packagegroup-core-boot, but in the future we may need a new packagegroup-core-tiny
#POKY_DEFAULT_EXTRA_RDEPENDS += "packagegroup-core-boot"
# Drop kernel-module-af-packet from RRECOMMENDS
POKY_DEFAULT_EXTRA_RRECOMMENDS = ""

# FIXME: what should we do with this?
TCLIBCAPPEND = ""

# Disable wide char support for ncurses as we don't include it in
# in the LIBC features below.
# Leave native enable to avoid build failures
ENABLE_WIDEC = "false"
ENABLE_WIDEC:class-native = "true"

# Drop native language support. This removes the
# eglibc->bash->gettext->libc-posix-clang-wchar dependency.
USE_NLS="no"
# As we don't have native language support, don't install locales into images
IMAGE_LINGUAS = ""

# Comment out any of the lines below to disable them in the build
# DISTRO_FEATURES options:
# alsa bluetooth ext2 pcmcia usbgadget usbhost wifi nfs zeroconf pci
DISTRO_FEATURES_HMS = "alsa bluetooth nfs pci"
DISTRO_FEATURES_NET = "ipv4"
DISTRO_FEATURES_USB = "usbhost"
DISTRO_FEATURES_WIFI = "wifi"

DISTRO_FEATURES = "${DISTRO_FEATURES_HMS} \
                   ${DISTRO_FEATURES_NET} \
                   ${DISTRO_FEATURES_USB} \
                   ${DISTRO_FEATURES_USBGADGET} \
                   ${DISTRO_FEATURES_WIFI} \
                  "

DISTRO_FEATURES:class-native = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES}"
DISTRO_FEATURES:class-nativesdk = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES}"

# Ensure the kernel nfs server is enabled
KERNEL_FEATURES:append:pn-linux-yocto = " features/nfsd/nfsd-enable.scc"

# enable systemd for init
POKY_INIT_MANAGER:poky-hms = "systemd"
# systemd isn't suitable with musl
POKY_INIT_MANAGER:poky-hms:libc-musl = "sysvinit"

# FIXME: Consider adding "modules" to MACHINE_FEATURES and using that in
# packagegroup-core-base to select modutils-initscripts or not.  Similar with "net" and
# netbase.

# # By default we only support initramfs. We don't build live as that
# # pulls in a lot of dependencies for the live image and the installer, like
# # udev, grub, etc.  These pull in gettext, which fails to build with wide
# # character support.
# IMAGE_FSTYPES = "cpio.gz"
# QB_DEFAULT_FSTYPE = "cpio.gz"

# Drop v86d from qemu dependency list (we support serial)
# Drop grub from meta-intel BSPs
# FIXME: A different mechanism is needed here. We could define -tiny
#        variants of all compatible machines, but that leads to a lot
#        more machine configs to maintain long term.
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = ""

# The mtrace script included by eglibc is a perl script. This means the system
# will build perl in case this package is installed. Since we don't care about
# this script for the purposes of tiny, remove the dependency from here.
RDEPENDS:${PN}-mtrace:pn-eglibc = ""

SKIP_RECIPE[build-appliance-image] = "not buildable with poky-hms"
SKIP_RECIPE[core-image-rt] = "not buildable with poky-hms"
SKIP_RECIPE[core-image-rt-sdk] = "not buildable with poky-hms"
SKIP_RECIPE[core-image-sato] = "not buildable with poky-hms"
SKIP_RECIPE[core-image-sato-dev] = "not buildable with poky-hms"
SKIP_RECIPE[core-image-sato-sdk] = "not buildable with poky-hms"
# SKIP_RECIPE[core-image-x11] = "not buildable with poky-hms"
# SKIP_RECIPE[core-image-weston] = "not buildable with poky-hms"

# Disable python usage in opkg-utils since it won't build with tiny config
PACKAGECONFIG:remove:pn-opkg-utils = "python"

# If shadow-base is brought into the image, logins will fail because it
# doesn't have the heuristics to work when CONFIG_MULTIUSER is unset.
PACKAGE_EXCLUDE += "shadow-base"
